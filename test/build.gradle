plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '1.0.0'

java {
    sourceCompatibility = '21'
}

configurations {
    testIntegrationImplementation.extendsFrom testImplementation
    testIntegrationMvcImplementation.extendsFrom testImplementation
}

sourceSets {
    'test-integration' {
        java.srcDirs = ['src/test-integration/java']
        resources.srcDirs = ['src/test-integration/resources']
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
    'test-integration-mvc' {
        java.srcDirs = ['src/test-integration-mvc/java']
        resources.srcDirs = ['src/test-integration-mvc/resources']
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.0"
    }
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
    implementation 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.mock-server:mockserver-spring-test-listener:5.15.0'
}

tasks.register('test-integration', Test) {
    shouldRunAfter test
    classpath = sourceSets.'test-integration'.runtimeClasspath
    testClassesDirs = sourceSets.'test-integration'.output.classesDirs
}

tasks.register('test-integration-mvc', Test) {
    shouldRunAfter test
    classpath = sourceSets.'test-integration-mvc'.runtimeClasspath
    testClassesDirs = sourceSets.'test-integration-mvc'.output.classesDirs
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('test-integration') {
    useJUnitPlatform()
}

tasks.named('test-integration-mvc') {
    useJUnitPlatform()
}

check {
    dependsOn 'test-integration', 'test-integration-mvc'
}
